<?xml version="1.0" encoding="UTF-8" standalone="no"?>
<event category="OTHER" id="23">
  <actions>
    <action id="603" library="1">
      <!--action name: Code-->
      <kind>CODE</kind>
      <allowRelative>false</allowRelative>
      <question>false</question>
      <canApplyTo>true</canApplyTo>
      <actionType>CODE</actionType>
      <functionName/>
      <relative>false</relative>
      <not>false</not>
      <appliesTo>.self</appliesTo>
      <arguments>
        <argument kind="STRING">{
    receiveCompleteMessage(global.serverSocket,4,global.deserializeBuffer);
    keyState = read_ubyte(global.deserializeBuffer);
    aimDirection = read_ushort(global.deserializeBuffer)*360/65536;
    aimDistance = read_ubyte(global.deserializeBuffer)*2;
    
    var temp, newIntel;
    if(global.updateType == QUICK_UPDATE) or (global.updateType == FULL_UPDATE) {
        receiveCompleteMessage(global.serverSocket,12,global.deserializeBuffer);
        x = read_ushort(global.deserializeBuffer)/5;
        y = read_ushort(global.deserializeBuffer)/5;
        hspeed = read_byte(global.deserializeBuffer)/8.5;
        vspeed = read_byte(global.deserializeBuffer)/8.5;
        xprevious = x;
        yprevious = y;
        
        hp = read_ubyte(global.deserializeBuffer);
        ammo[0] = read_ubyte(global.deserializeBuffer);
        ammo[1] = read_ubyte(global.deserializeBuffer);
        
        // overheal
        overhealHp = read_byte(global.deserializeBuffer);
        overhealed = read_ubyte(global.deserializeBuffer);
        
        temp = read_ubyte(global.deserializeBuffer);
        cloak = (temp &amp; $01 != 0);
        moveStatus = (temp &gt;&gt; 1) &amp; $07;
    }

if(global.updateType == FULL_UPDATE){
        receiveCompleteMessage(global.serverSocket,12,global.deserializeBuffer);
        animationOffset = read_ubyte(global.deserializeBuffer);
        
        // track whether holding primary/secondary
        activeWeapon = read_ubyte(global.deserializeBuffer);
        //class specific syncs
        switch(player.class)
        {
        case CLASS_SPY:
            cloakAlpha = read_ubyte(global.deserializeBuffer)/255;
            break;
        case CLASS_MEDIC:
            uberChargeC = read_ubyte(global.deserializeBuffer)*2000/255;
            break;
        case CLASS_ENGINEER:
            nutsNBolts = read_ubyte(global.deserializeBuffer);
            break;
        case CLASS_SNIPER:
            currentWeapon.t = read_ubyte(global.deserializeBuffer);
            if(currentWeapon.t) zoomed = true; // Quick hack
            break;
        default:
            read_ubyte(global.deserializeBuffer);
        }
        alarm[1] = read_short(global.deserializeBuffer)/global.delta_factor; 
        if (alarm[1] != 0)
            canGrabIntel = false;
        intel = read_ubyte(global.deserializeBuffer);
        intelRecharge = read_short(global.deserializeBuffer);
        
        // track the primary/secondary
        weapons[0] = read_ubyte(global.deserializeBuffer);
        weapons[1] = read_ubyte(global.deserializeBuffer);
        player.weapons[0] = weapons[0];
        player.weapons[1] = weapons[1];
        
        // switch to primary/secondary
        weaponSwitch(weapons[activeWeapon]);

        currentWeapon.readyToShoot = read_ubyte(global.deserializeBuffer);
        currentWeapon.alarm[0] = read_ubyte(global.deserializeBuffer)/global.delta_factor;
        
        //weapon specific syncs
        if (weaponType[0] == MINEGUN)
        {
            var i, mine;
            receiveCompleteMessage(global.serverSocket, 1, global.deserializeBuffer);
            lobbed[0] = read_ubyte(global.deserializeBuffer);
    
            with(specialProjectile[0]) {
                if (variable_local_exists("ownerPlayer")) {
                    if(ownerPlayer == other.player) {
                        instance_destroy();
                    }
                }
            }
            
            for(i=0; i&lt;lobbed[0]; i+=1) {
                mine = instance_create(0,0,specialProjectile[0]);
                mine.owner = id;
                mine.ownerPlayer = player;
                mine.team = player.team;
                with(mine) {
                    event_user(13);
                }
            }
        }
    }
    event_user(1);
}


</argument>
      </arguments>
    </action>
  </actions>
</event>
